# ----------------------------------------------------------------------
#  RAPPTURE OBJECT: boolean
#
#  A logical on/off value.  It is usually used as an input, but it
#  can also be an output from a simulation.
#
# ======================================================================
#  AUTHOR:  Michael McLennan, Purdue University
#  Copyright (c) 2004-2012  HUBzero Foundation, LLC
#
#  See the file "license.terms" for information on usage and
#  redistribution of this file, and for a DISCLAIMER OF ALL WARRANTIES.
# ======================================================================

object boolean -extends base {
    palettes "Inputs" "Outputs"

    help http://rappture.org/wiki/rp_xml_ele_boolean

    attr default -title "Default Value" -type boolean -path default -only input -tooltip "Sets the value that this input has when the program first starts.  This value is used by default unless the user explicitly changes it."

    check default {
        if {[string length [string trim $attr(default)]] == 0} {
            return [list error "Must have a default value for this boolean value."]
        }
    }

    storage {
        private variable _val   ;# boolean value
    }

    import xml {xmlobj path} {
        attr import $xmlobj $path
        import_string [$xmlobj get $path.current]
    }

    export xml {xmlobj path} {
        set v [expr {($_val) ? "true" : "false"}]
        $xmlobj put $path $v
    }

    import string {val} {
        if {[string is boolean -strict $val]} {
            set _val $val
        } else {
            error "don't recognize value"
        }
    }

    export string {var} {
        upvar $var v
        set v [expr {($_val) ? "true" : "false"}]
    }

    compare {
        if {$_val && !$_val2} {
            return 1
        } elseif {!$_val && $_val2} {
            return -1
        }
        return 0
    }
}
